//Gradle build for core LaTiS code.
//Make sure version is correct then run "gradle publish"

apply plugin: 'scala'
apply plugin: 'war'
apply plugin: 'ivy-publish'

//Define the release version. Used for published jar.
//TODO: get from latis.properties?
version = '2.3.11'

//Define Java version for generated class files.
targetCompatibility = 1.6

//Define the Ivy repository for publishing build artifacts.
group = 'lasp'
publishing {
    publications {
        ivyJava(IvyPublication) {
            from components.java
        }
    }
    repositories {
        ivy {
            url "/data_systems/ivy"
        }
    }
}

//Repositories for dependencies.
repositories {
    ivy {
      url "http://dsweb:8080/ivy"
    }
    mavenCentral()
}

dependencies {    
    providedCompile "javax.servlet:servlet-api:2.5" //not deployed
    compile 'org.scala-lang:scala-library:2.10.3'
    
    compile 'joda-time:joda-time:2.3'
    
    compile 'com.typesafe:scalalogging-slf4j_2.10:1.0.1'
    compile 'ch.qos.logback:logback-classic:1.0.13'
    
    testCompile 'junit:junit:4.+'
    testCompile 'org.apache.derby:derby:10.10.1.1'
    //testRuntime 'postgres:postgresql:9.2-1002'
    testRuntime 'sybase:jconn3:6.0'
	
	compile 'jfree:jfreechart:1.0.12'
}

//For testing
task deploy(dependsOn: 'war') << {
    copy {
        from war.archivePath
        //into "/opt/glassfish3/glassfish/domains/domain1/autodeploy"
        into "/opt/tomcat/webapps"
    }
}
